BACKUP ~cdtweaks_helper/backup~ // location to store files for
AUTHOR ~pcamagna@yahoo.com~ // email address displayed if install fails

VERSION ~v2021.10.20~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// generate armor list                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN ~generate armor list for Tweaks Anthology~
NO_LOG_RECORD

<<<<<<<<./inline/descript.log
>>>>>>>>

ACTION_IF FILE_EXISTS ~cdtweaks/2da/common_armor_list.2da~ BEGIN

  COPY ~cdtweaks/2da/common_armor_list.2da~ ~cdtweaks_helper/email_to_cam/common_armor_list.2da~
  
END  

ACTION_IF FILE_EXISTS ~weidu-bgee.log~ BEGIN

  COPY ~weidu-bgee.log~ ~cdtweaks_helper/email_to_cam/weidu-bgee.log~
  
END  

COPY ~weidu.log~             ~cdtweaks_helper/email_to_cam/weidu.log~
     ~./inline/descript.log~ ~cdtweaks_helper/email_to_cam/descript.log~
     ~./inline/descript.log~ ~cdtweaks_helper/email_to_cam/install.tpa~

COPY ~cdtweaks_helper/email_to_cam/common_armor_list.2da~ ~cdtweaks_helper/email_to_cam/common_armor_list.2da~
  COUNT_2DA_ROWS 11 rows
  FOR (row = 1 ; row < rows ; ++row) BEGIN
    READ_2DA_ENTRY row 0 11 item
    TO_LOWER item
    SET $armor("%item%") = 1
  END  
  REPLACE_TEXTUALLY ~^\([^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]*\)$~ ~\1 ****~
  PRETTY_PRINT_2DA

  REPLACE_EVALUATE 
    ~^\([^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+\)\(.+\)$~
    BEGIN
      INNER_PATCH_SAVE MATCH2 ~%MATCH2%~ BEGIN
        REPLACE_TEXTUALLY ~ +~ ~ ~
      END
   END ~%MATCH1%%MATCH2%~    

OUTER_SET email = 0
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~cdtweaks_helper/email_to_cam~
  READ_SHORT 0x1c type  ELSE 0
  READ_BYTE  0x18 flags ELSE 0
  SPRINT item ~%SOURCE_RES%~
  TO_LOWER item
  PATCH_IF (((type = 2) OR (type = 12)) AND            // type is armor or shield
            ((flags & BIT2) = BIT2)     AND            // is flagged as droppable
            (!VARIABLE_IS_SET $armor("%item%"))) BEGIN // not already in the list
            
    SET email = 1
    WRITE_SHORT 0x1c THIS + 100 // make an arbitrary change to force the copy
    SET name_spc_exists = 0
    READ_LONG 0x0c valid
    PATCH_IF (valid < 2147483646) AND (valid >= 0) BEGIN
      READ_STRREF 0x08 name_gen ELSE ""
      READ_STRREF 0x0c name_spc
      SET name_spc_exists = 1
    END ELSE BEGIN
      READ_STRREF 0x08 name_gen ELSE "no name"
      SPRINT name_spc ""
    END
    SET desc_spc_exists = 0
    READ_LONG 0x54 valid
    PATCH_IF (valid < 2147483646) AND (valid >= 0) BEGIN
      READ_STRREF 0x50 desc_gen ELSE ""
      READ_STRREF 0x54 desc_spc
      SET desc_spc_exists = 1
    END ELSE BEGIN
      READ_STRREF 0x50 desc_gen ELSE "no desc"
      SPRINT desc_spc ""
    END
    READ_ASCII 0x22 icon (2)
    PATCH_IF ("%icon%" STRING_COMPARE_CASE "2A" = 0) BEGIN
      SPRINT "appear" "Leather armor"
    END ELSE
    PATCH_IF ("%icon%" STRING_COMPARE_CASE "3A" = 0) BEGIN
      SPRINT "appear" "Chain mail"
    END ELSE
    PATCH_IF ("%icon%" STRING_COMPARE_CASE "4A" = 0) BEGIN
      SPRINT "appear" "Plate mail"
    END ELSE
    PATCH_IF ("%icon%" STRING_COMPARE_REGEXP "[234]W" = 0) BEGIN
      SPRINT "appear" "Robe"
    END ELSE
    PATCH_IF ("%icon%" STRING_COMPARE_CASE "D1" = 0) BEGIN
      SPRINT "appear" "Buckler"
    END ELSE
    PATCH_IF ("%icon%" STRING_COMPARE_CASE "D2" = 0) BEGIN
      SPRINT "appear" "Small shield"
    END ELSE
    PATCH_IF ("%icon%" STRING_COMPARE_CASE "D3" = 0) BEGIN
      SPRINT "appear" "Medium shield"
    END ELSE
    PATCH_IF ("%icon%" STRING_COMPARE_CASE "D4" = 0) BEGIN
      SPRINT "appear" "Large/Tower shield"
    END ELSE BEGIN
      SPRINT "appear" "Unknown"
    END
    READ_LONG  0x6a fx_off
    READ_SHORT 0x70 fx_num
    SPRINT stlth  "Yes"
    SPRINT thving "Yes"
    SPRINT arcne  "Yes"
    FOR (index = 0 ; index < fx_num ; ++index) BEGIN
      READ_SHORT (fx_off +        (index * 0x30)) opcode
      READ_LONG  (fx_off + 0x08 + (index * 0x30)) button
      PATCH_IF ((opcode = 144) AND (button = 0)) BEGIN
        SPRINT stlth  "No"
      END
      PATCH_IF ((opcode = 144) AND (button = 1)) BEGIN
        SPRINT thving  "No"
      END
      PATCH_IF ((opcode = 145) AND (button = 0)) BEGIN
        SPRINT arcne  "No"
      END
    END
    INNER_ACTION BEGIN

APPEND_OUTER ~cdtweaks_helper/email_to_cam/install.tpa~
"%WNL%%WNL%
COPY ~cdtweaks_helper/email_to_cam/%item%.itm~ ~override~
  WRITE_SHORT 0x1c THIS - 100 // undo arbitrary change
  SAY 0x08 ~%name_gen%~
  SAY 0x50 ~%desc_gen%~"
ACTION_IF name_spc_exists BEGIN APPEND_OUTER ~cdtweaks_helper/email_to_cam/install.tpa~ "  SAY 0x0c ~%name_spc%~" END ELSE BEGIN 
                                APPEND_OUTER ~cdtweaks_helper/email_to_cam/install.tpa~ ~  WRITE_LONG 0x0c "-1"~ END
ACTION_IF desc_spc_exists BEGIN APPEND_OUTER ~cdtweaks_helper/email_to_cam/install.tpa~ "  SAY 0x54 ~%desc_spc%~" END ELSE BEGIN 
                                APPEND_OUTER ~cdtweaks_helper/email_to_cam/install.tpa~ ~  WRITE_LONG 0x54 "-1"~ END
  
APPEND_OUTER ~cdtweaks_helper/email_to_cam/descript.log~
"%WNL%%WNL%
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
  ~%item%~ => 10000 // %name_spc% (%name_gen%)
Equipped appearance: %appear%
Cast arcane? %arcne%  
Stealth? %stlth%  
Thieving? %thving%
%WNL%
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
%WNL%
%desc_spc% 
%WNL%
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
%WNL%
%desc_gen%
%WNL%"

    END // end inner_action
  END
  BUT_ONLY

ACTION_IF email BEGIN
  PRINT 
~
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

New items were found! Please zip/rar the contents of the email_to_cam 
folder, located inside the cdtweaks_helper folder, to pcamagna@yahoo.com.

Thanks!

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\~

END ELSE BEGIN

PRINT
~
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

No new items found! Thanks for running the tool; all of the items found
are already covered by Tweaks Anthology. No further action is necessary.

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\~

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// for cam to test these locally                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

//BEGIN ~Cam installs locally~
//INCLUDE ~cdtweaks/email_to_cam/install.tpa~